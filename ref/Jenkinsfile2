#!/usr/bin/env groovy
@Library('Common') _
def common = new Common()

pipeline {
  agent any
  options {
    timestamps()
    buildDiscarder(logRotator(numToKeepStr: common.NUM_TO_KEEP_STR))
    timeout(time: common.TIMEOUT_IN_MINUTES, unit: 'MINUTES')
    skipDefaultCheckout(true)
    retry(common.NUM_RETRIES)
    quietPeriod(common.QUIET_PERIOD_IN_SECONDS)
  }

  parameters {
    choice(name: 'MAVEN_REPO_ID', choices: ["${MAVEN_STAGE_REPO_ID}", "${MAVEN_RELEASE_REPO_ID}"], description: 'Select the nexus repo')
    string(name: 'MAVEN_ARTIFACT_ID', defaultValue: 'institution', description: 'Artifact to deploy')
    string(name: 'MAVEN_GROUP_ID', defaultValue: 'io.vaulttec.wallet', description: 'Group of artifact to promote')
    string(name: 'MAVEN_VERSION', defaultValue: '0.0.1-SNAPSHOT', description: 'Version to deploy')
    string(name: 'SERVICE_NAME', defaultValue: 'sbi-inst', description: 'Service name')
    choice(name: 'ENVIRONMENT', choices: ['inst', 'ttp'], description: 'Which environment should the artifact be deployed')
  }

  stages {
    stage('Checkout SCM') {
      steps {
        script {
          withCredentials([usernamePassword(credentialsId: 'nexus_pass', usernameVariable: 'NEXUS_USER', passwordVariable: 'NEXUS_PASS')]) {
            env.NEXUS_USER = "${NEXUS_USER}"
            env.NEXUS_PASS = "${NEXUS_PASS}"
          }

          deleteDir()
          def scmVars = checkout scm
          env.SCM_GIT_BRANCH = "${scmVars.GIT_BRANCH}"
          common.setEnvVar(this, env.SCM_GIT_BRANCH)

          env.MAVEN_REPO_URL = "${NEXUS_LOCATION}/nexus/repository/${MAVEN_REPO_ID}/" + "${MAVEN_GROUP_ID}".replace('.', '/')
          env.MAVEN_APP_METADATA_URL = "${MAVEN_REPO_URL}/${MAVEN_ARTIFACT_ID}/${MAVEN_VERSION}/maven-metadata.xml"

          switch (env.ENVIRONMENT) {
            case 'ttp':
              env.ENVIRONMENT_IP = "${TTP_BOX_IP}"
              break
            default:
              env.ENVIRONMENT_IP = "${INST_BOX_IP}"
          }
        }
      }
    }

    stage('Deploy jar') {
      steps {
        script {
          print """
==================================================================================
Repo ID: "${MAVEN_REPO_ID}"
Artifact ID: "${MAVEN_ARTIFACT_ID}"
Service Name: "${SERVICE_NAME}"
Version: "${MAVEN_VERSION}"
Environment: "${ENVIRONMENT}"
Environment IP: "${ENVIRONMENT_IP}"

Maven Repo URL: "${MAVEN_REPO_URL}"
Maven Metadata URL: "${MAVEN_APP_METADATA_URL}"
==================================================================================
"""
          common.slack(this, "STARTED: <${BUILD_URL}|Build ${BUILD_NUMBER}> to deploy version ${MAVEN_VERSION} from ${MAVEN_REPO_ID}"
            , 'good', 'events')

          sshagent(credentials: ['bitbucket_ssh']) {
            common.deploy(this, env.MAVEN_ARTIFACT_ID, env.SERVICE_NAME, env.ENVIRONMENT_IP, env.MAVEN_REPO_URL, env.MAVEN_VERSION)
          }

          common.slack(this, "DEPLOYED: \"${MAVEN_ARTIFACT_ID}\" service built from \"${SCM_GIT_BRANCH}\" was deployed to ${ENVIRONMENT_IP}"
            , 'warning', 'events')
        }
      }
    }

  }

  post {
    success {
      script {
        common.slack(this, "COMPLETE: <${BUILD_URL}|Build ${BUILD_NUMBER}> deployed successfully"
          , 'good', 'events')
      }
    }
    failure {
      script {
        common.slack(this, "FAIL: <${BUILD_URL}/console|Build ${BUILD_NUMBER}> failed to deploy"
          , 'danger', 'fail')
        common.slack(this, "FAIL: <${BUILD_URL}/console|Build ${BUILD_NUMBER}> failed to deploy"
          , 'danger', 'events')
      }
    }
  }

}
